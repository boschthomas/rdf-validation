# DATA-CUBE-C-VALUE-IS-VALID-FOR-DATATYPE-01: Datatype consistency (IC-0)
# -----
# The RDF graph must be consistent under RDF D-entailment using a datatype map containing all the datatypes used within the graph.

PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX qb:   <http://purl.org/linked-data/cube#> .
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 

	BIND ( ( 'ERROR' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-EXACT-QUALIFIED-CARDINALITY-RESTRICTIONS-01: Unique data set (IC-1)
# -----
# Every qb:Observation has (qb:dataSet) exactly one associated qb:DataSet

PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
	{
		?subject a qb:Observation .	
		FILTER NOT EXISTS { ?subject qb:dataSet ?dataSet . }
	}
	UNION
	{
		?subject 
		    a qb:Observation ;
		    qb:dataSet ?dataSet1 , ?dataSet2 .
        FILTER ( ?dataSet1 != ?dataSet2 )
	}

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-EXACT-QUALIFIED-CARDINALITY-RESTRICTIONS-01: Unique data set (IC-1)" )
		AS ?violationMessage ) . 
	BIND ( ( 'ERROR' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-EXACT-QUALIFIED-CARDINALITY-RESTRICTIONS-02: Unique DSD (IC-2)
# -----
# Every qb:DataSet has (qb:structure) exactly one associated qb:DataStructureDefinition

PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
	{
		?subject a qb:DataSet .	
		FILTER NOT EXISTS { ?subject qb:structure ?dsd . }
	}
	UNION
	{
		?subject 
		    a qb:DataSet ;
		    qb:structure ?dsd1 , ?dsd2 .
        FILTER ( ?dsd1 != ?dsd2 )
	}

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-EXACT-QUALIFIED-CARDINALITY-RESTRICTIONS-02: Unique DSD (IC-2)" )
		AS ?violationMessage ) . 
	BIND ( ( 'ERROR' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-05: DSD includes measure (IC-3)
# -----
# Every qb:DataStructureDefinition must include (qb:component, qb:componentProperty) at least one declared measure.

PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
	?subject a qb:DataStructureDefinition .
    FILTER NOT EXISTS { ?subject qb:component [ qb:componentProperty [ a qb:MeasureProperty ] ] }

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-05: DSD includes measure (IC-3)" )
		AS ?violationMessage ) . 
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-01: Dimensions have range (IC-4)
# -----
# Every dimension declared in a qb:DataStructureDefinition must have a declared rdfs:range.

PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
	?subject a qb:DimensionProperty .
    FILTER NOT EXISTS { ?subject rdfs:range [] . }

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-01: Dimensions have range (IC-4)" )
		AS ?violationMessage ) . 
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-02: Concept dimensions have code lists (IC-5)
# -----
# Every dimension with range skos:Concept must have a qb:codeList.

PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
	?subject a qb:DimensionProperty ;
        rdfs:range skos:Concept .
    FILTER NOT EXISTS { ?subject qb:codeList [] . }

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-02: Concept dimensions have code lists (IC-5)" )
		AS ?violationMessage ) . 
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-DATA-MODEL-CONSISTENCY-01: Only attributes may be optional (IC-6)
# -----
# The only components of a qb:DataStructureDefinition that may be marked as optional, using qb:componentRequired are attributes.

PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
    ?subject qb:component ?componentSpec .
    ?componentSpec qb:componentRequired "false"^^xsd:boolean ;
        qb:componentProperty ?component .
    FILTER NOT EXISTS { ?component a qb:AttributeProperty . }

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-DATA-MODEL-CONSISTENCY-01: Only attributes may be optional (IC-6)" )
		AS ?violationMessage ) . 
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}


# DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-04: Slice Keys must be declared (IC-7)
# -----
# Every qb:SliceKey must be associated with (qb:sliceKey) a qb:DataStructureDefinition

PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX qb:   <http://purl.org/linked-data/cube#> 
PREFIX :     <http://data#>
SELECT ?subject ?violationMessage ?severityLevel
WHERE {	 
    ?subject a qb:SliceKey .
    FILTER NOT EXISTS { [ a qb:DataStructureDefinition ] qb:sliceKey ?subject }

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"DATA-CUBE-C-EXISTENTIAL-QUANTIFICATIONS-04: Slice Keys must be declared (IC-7)" )
		AS ?violationMessage ) . 
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}