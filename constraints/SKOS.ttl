PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf:	<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <http://www.w3.org/2004/02/skos/core/data#>


# Labeling and Documentation Issues - Omitted or Invalid Language Tags
# -----
SELECT ?s ?violationMessage
WHERE {	 
	?s 
	    a [rdfs:subClassOf* skos:Concept] ;
	    ?p ?l . 
	BIND ( ( LANG ( ?l ) ) AS ?language ) . 
	
	# validation
	# ----------
	FILTER ( ?language = "" ) . 

    # violation message
    # -----
	BIND ( CONCAT( 
		"Omitted or invalid language tags of text literals | The language tag of the text literal '", 
		xsd:string( ?l ), 
		"' is omitted.") 
		AS ?violationMessage ) . 
}

# Labeling and Documentation Issues - Incomplete Language Coverage
# -----
SELECT ?s ?violationMessage
WHERE {	  	
	# data
	# ----
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		?p ?label1 .
	FILTER ( ?p != rdf:type ) .
	BIND ( ( LANG ( ?label1 ) ) AS ?language1 ) .
	FILTER ( ?language1 = "en" ) .

	# validation
	# ----------	
	{
		SELECT ( COUNT ( ?s ) AS ?en)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "en" ) .
		}
	}
	{
		SELECT ( COUNT ( ?s ) AS ?de)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "de" ) .
		}
	}
	{
		SELECT ( COUNT ( ?s ) AS ?fr)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "fr" ) .
		}
	}
	FILTER ( ?de = 0 || ?fr = 0 ) .

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"The languages en, de, fr should be covered by each property of each SKOS concept | ",
		"The property '",
		xsd:string( ?p ),
		"' has ",
		xsd:string( ?en ),
		" english value(s), ",
		xsd:string( ?de ),
		" german value(s), and ",
		xsd:string( ?fr ),
		" french value(s)",
		".") 
		AS ?violationMessage ) . 
}

SELECT ?s ?violationMessage
WHERE {	  	
	# data
	# ----
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		?p ?label1 .
	FILTER ( ?p != rdf:type ) .
	BIND ( ( LANG ( ?label1 ) ) AS ?language1 ) .
	FILTER ( ?language1 = "de" ) .

	# validation
	# ----------	
	{
		SELECT ( COUNT ( ?s ) AS ?en)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "en" ) .
		}
	}
	{
		SELECT ( COUNT ( ?s ) AS ?de)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "de" ) .
		}
	}
	{
		SELECT ( COUNT ( ?s ) AS ?fr)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "fr" ) .
		}
	}
	FILTER ( ?en = 0 || ?fr = 0 ) .

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"The languages en, de, fr should be covered by each property of each SKOS concept | ",
		"The property '",
		xsd:string( ?p ),
		"' has ",
		xsd:string( ?en ),
		" english value(s), ",
		xsd:string( ?de ),
		" german value(s), and ",
		xsd:string( ?fr ),
		" french value(s)",
		".") 
		AS ?violationMessage ) . 
}

SELECT ?s ?violationMessage
WHERE {	  	
	# data
	# ----
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		?p ?label1 .
	FILTER ( ?p != rdf:type ) .
	BIND ( ( LANG ( ?label1 ) ) AS ?language1 ) .
	FILTER ( ?language1 = "fr" ) .

	# validation
	# ----------	
	{
		SELECT ( COUNT ( ?s ) AS ?en)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "en" ) .
		}
	}
	{
		SELECT ( COUNT ( ?s ) AS ?de)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "de" ) .
		}
	}
	{
		SELECT ( COUNT ( ?s ) AS ?fr)
		WHERE {
			?s ?p ?literal1 .
			BIND ( ( LANG ( ?literal1 ) ) AS ?language1 ) .
			FILTER ( ?language1 = "fr" ) .
		}
	}
	FILTER ( ?en = 0 || ?de = 0 ) .

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"The languages en, de, fr should be covered by each property of each SKOS concept | ",
		"The property '",
		xsd:string( ?p ),
		"' has ",
		xsd:string( ?en ),
		" english value(s), ",
		xsd:string( ?de ),
		" german value(s), and ",
		xsd:string( ?fr ),
		" french value(s)",
		".") 
		AS ?violationMessage ) . 
}

# Labeling and Documentation Issues - No Common Language
# -----
SELECT ?s ?violationMessage
WHERE {	
	# data
	# ----
	?s
		a [rdfs:subClassOf* skos:Concept] .

	# validation
	# ----------
	FILTER NOT EXISTS {
		?s ?p ?l .
		BIND ( ( LANG ( ?l ) ) AS ?lang ) .
		FILTER ( ?lang = "en" ) .
	}

	# violation message
	# -----------------
	BIND ( CONCAT( 
		"Each SKOS concept must have at least 1 literal in the same common language (en) | ",
		"The SKOS concept '",
		xsd:string( ?s ),
		"' has 0 literals in the same common language (en).",
		"") 
		AS ?violationMessage ) . 
}

# Labeling and Documentation Issues - Undocumented Concepts
# -----
SELECT ?s ?violationMessage
WHERE {	  	
	# data
	# ----
	?s a ?class .
	?class rdfs:subClassOf* skos:Concept .
	
	# validation
	# ----------
	FILTER NOT EXISTS { 
		{ { ?s skos:note ?x } UNION { ?s skos:changeNote ?x } . }
		UNION 
		{ { ?s skos:definition ?x } UNION { ?s skos:editorialNote ?x } . }
		UNION 
		{ { ?s skos:example ?x } UNION { ?s skos:historyNote ?x } . }
		UNION 
		{ ?s skos:scopeNote ?x } .
	}  

	# violation message
	# -----
	BIND ( CONCAT( 
		"The concept '", 
		xsd:string( ?s ), 
		"' does not use one of skos:note, skos:changeNote, skos:definition, skos:editorialNote, skos:example, skos:historyNote, or skos:scopeNote.") 
		AS ?violationMessage ) . 
}

# Labeling and Documentation Issues - Overlapping Labels
# -----
SELECT ?s ?violationMessage
WHERE {		
	# data
	# ----
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		skos:inScheme ?conceptScheme ;
		?p ?label1 .
	BIND ( ( STR ( ?label1 ) ) AS ?l1 ) .
	BIND ( ( LANG ( ?label1 ) ) AS ?lang1 ) .
	?concept 
		a [rdfs:subClassOf* skos:Concept] ;
		skos:inScheme ?conceptScheme ;
		?p ?label2 .
	BIND ( ( STR ( ?label2 ) ) AS ?l2 ) .
	BIND ( ( LANG ( ?label2 ) ) AS ?lang2 ) .
	?conceptScheme
		a [rdfs:subClassOf* skos:ConceptScheme] .
		
	# validation
	# ----------
	FILTER ( ?this != ?concept ) .
	FILTER ( ?l1 = ?l2 ) .
	FILTER ( ?lang1 = ?lang2 ) . 

	# violation message
	# -----
	BIND ( CONCAT( 
		"Two SKOS concepts cannot have the same preferred lexical label in a given language when they belong to the same SKOS concept scheme | '", 
		"The SKOS concept '", 
		xsd:string( ?s ), 
		"' and the SKOS concept '",
		xsd:string( ?concept ), 
		"', belonging to the same concept scheme '",
		xsd:string( ?conceptScheme ), 
		"', have the same preferred lexical label '",
		xsd:string( ?l1 ), 
		"' in the given language '",
		xsd:string( ?lang1 ), 
		"'.'"
		) AS ?violationMessage ) . 
}
			
# Labeling and Documentation Issues - Missing Labels
# -----
SELECT ?s ?violationMessage
WHERE {	  	
	# data
	# ----
	{ { ?s a [rdfs:subClassOf* skos:Concept] } UNION { ?s a [rdfs:subClassOf* skos:ConceptScheme] } . }
	UNION 
	{ { ?s a [rdfs:subClassOf* skos:Collection] } UNION { ?s a [rdfs:subClassOf* skos:OrderedCollection] } . }

	# validation
	# ----------
	FILTER NOT EXISTS {
		{ { ?s skos:prefLabel ?l } UNION { ?s skos:altLabel ?l } . }
		UNION 
		{ { ?s rdfs:label ?l } UNION { ?s dcterms:title ?l } . }
	} 

	# violation message
	# -----
	BIND ( CONCAT( 
		"SKOS classes ( Concept, ConceptScheme, Collection, OrderedCollection ) should be labeled with e.g. skos:prefLabel, skos:altLabel, rdfs:label, dcterms:title '", 
		"( to make the vocabulary more convenient for humans to use ) | ",  
		xsd:string( ?s ), 
		"' is not labeled."
		) AS ?violationMessage ) . 
}

# Labeling and Documentation Issues - Empty Labels
# -----
SELECT ?s ?p ?violationMessage
WHERE {	  	
	# data
	# ----
	?s ?p ?literal .
	BIND ( STR ( ?literal ) AS ?l ) .

	# validation
	# ----------
	FILTER ( ?l = "" ) . 

	# violation message
	# -----
    BIND ( CONCAT( 
      "Labels are needed to contain textual information to be useful | The property '",  
      xsd:string( ?p ), 
      "' points to an empty label."
      ) AS ?violationMessage ) . 
}


# Labeling and Documentation Issues - Ambiguous Notation References
# -----
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX :     <http://data#>
SELECT ?s ?violationMessage ?severityLevel
WHERE {	 
	# data
	# ---- 
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		skos:inScheme ?cs ;
        skos:notation ?l1 .
	?concept 
		a [rdfs:subClassOf* skos:Concept] ;
		skos:inScheme ?cs ;
        skos:notation ?l1 .
	
	# validation
	# ----------
    #FILTER ( ?cs = ?conceptScheme ) .
    FILTER ( ?s != ?concept ) .
    #FILTER ( ?l1 = ?l2 ) .
	
	# violation message
	# -----
	BIND ( STR ( ?l1 ) AS ?l1_str ) .
	BIND ( CONCAT( 
		"Concepts within the same concept scheme should not have identical skos:notation literals | The concept '",  
		xsd:string( ?s ), 
		"' and the concept '",
		xsd:string( ?concept ), 
		"' within the same concept scheme '",
		xsd:string( ?cs ), 
		"' have an identical skos:notation literal '",
		xsd:string( ?l1_str ), 
		"'."
		) AS ?violationMessage ) . 
		
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}

# not permorming well:
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX :     <http://data#>
SELECT ?s ?violationMessage
WHERE {	  	
	# data
	# ----
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		skos:inScheme ?conceptScheme ;
		skos:notation ?l .
	?conceptScheme
		a [rdfs:subClassOf* skos:ConceptScheme] .
	{
		VALUES ?subject {?s}
	    {
			SELECT ( COUNT ( ?concept ) AS ?count )
			WHERE {
				?concept 
					a [rdfs:subClassOf* skos:Concept] ;
					skos:inScheme ?conceptScheme ;
					skos:notation ?l .
					FILTER ( ?subject != ?concept ) .
			}
		}
	}
		
	# validation
	# ----------
	#FILTER ( ?count != 0 ) .

	# violation message
	# -----
	BIND ( STR ( ?conceptScheme ) AS ?cs_str ) .
	BIND ( STR ( ?l ) AS ?l_str ) .
	BIND ( CONCAT( 
		"Concepts with the same concept scheme should not have identical skos:notation literals | The concept '",  
		xsd:string( ?s ), 
		"' and another concept with the same concept scheme '",
		xsd:string( ?cs_str ), 
		"' have the identical skos:notation literal '",
		xsd:string( ?l_str ), 
		"'."
		) AS ?violationMessage ) . 
}

# not permorming well:
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX :     <http://data#>
SELECT ?s ?violationMessage ?severityLevel
WHERE {	 
	# data
	# ---- 
	?s 
		a [rdfs:subClassOf* skos:Concept] ;
		skos:inScheme ?cs ;
        skos:notation ?l1 .
	
	# subquery
	# ----
	{
		SELECT ?concept ?conceptScheme ?l2
		WHERE {
			?concept 
				a [rdfs:subClassOf* skos:Concept] ;
				skos:inScheme ?conceptScheme ;
                skos:notation ?l2 .
		}
	} 
	
	# validation
	# ----------
    FILTER ( ?cs = ?conceptScheme ) .
    FILTER ( ?s != ?concept ) .
    FILTER ( ?l1 = ?l2 ) .
	
	# violation message
	# -----
	BIND ( STR ( ?l1 ) AS ?l1_str ) .
	BIND ( CONCAT( 
		"Concepts within the same concept scheme should not have identical skos:notation literals | The concept '",  
		xsd:string( ?s ), 
		"' and another concept within the same concept scheme '",
		xsd:string( ?cs ), 
		"' have the identical skos:notation literal '",
		xsd:string( ?l1_str ), 
		"'."
		) AS ?violationMessage ) . 
		
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}

# Structural Issues - Orphan Concepts
# -----
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX :     <http://data#>
SELECT ?s ?violationMessage ?severityLevel
WHERE {	 
	# data
	# ----
	?s a [rdfs:subClassOf* skos:Concept] .
		
	# validation
	# ----------
	FILTER NOT EXISTS {
		?s ?p ?o .
		FILTER ( ?p IN ( 
		skos:related,
		skos:relatedMatch,
		skos:broader,
		skos:broadMatch,
		skos:narrower,
		skos:narrowMatch,
		skos:closeMatch,
		skos:exactMatch,
		skos:relatedMatch,
		skos:broadMatch,
		skos:narrowMatch ) ) .	 
	}
	
	# violation message
	# -----
	BIND ( CONCAT( 
		"The concept '", 
		xsd:string( ?s ), 
		"' does not have any associative or hierarchical relations.",
		" | A controlled vocabulary that contains many orphan concepts is less usable for search and retrieval use cases. ") 
		AS ?violationMessage ) . 
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}


# Structural Issues - Disconnected Concept Clusters
# -----


# Structural Issues - Cyclic Hierarchical Relations
# -----
PREFIX rdf:	 <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX :     <http://data#>
SELECT ?s ?violationMessage ?severityLevel
WHERE {	
	# data
	# ----
	?s a [rdfs:subClassOf* skos:Concept] .
		
	# validation
	# ----------
	?s skos:broader* ?s .

	# violation message
	# -----
	BIND ( CONCAT( 
		"There is a cyclic relation for the concept '", 
		xsd:string( ?s ), 
		"'. | cyclic relations may reveal a logical problem in the thesaurus. ") 
		AS ?violationMessage ) .
	BIND ( ( 'INFO' ) AS ?severityLevel ) .
}